#!/usr/bin/fift -s
"TonUtil.fif" include
"GetOpt.fif" include
"utils.fif" include

{ ."usage: " @' $0 type ." <msg>" cr
  ."Displays internal message loaded from <msg>.boc"
  cr 1 halt
} : usage

$# 1 <> ' usage if

$1 +".boc" file>B B>boc <s
8 u@+ swap constant send-mode ref@ <s
1 i@+ swap { ."not an internal message" cr 1 halt } if
1 i@+ swap constant ihr-disabled
1 i@+ swap constant bounce
1 i@+ nip
2 u@+ swap 0 <> { ."src = none expected" cr 1 halt } if
addr@+ -rot 2constant dest
Gram@+ swap constant value
1 i@+ swap { ref@+ swap } { null } cond constant extra
Gram@+ nip Gram@+ nip
64 u@+ nip 32 u@+ nip
1 i@+ swap { ."StateInit displaying is not supported" cr 1 halt } if
1 i@+ swap { ref@ <s } if constant body

."Transferring " value extra .GR+cc ."to account "
dest 2dup bounce 5 + .Addr ." = " .addr cr
."ihr-disabled = " ihr-disabled . cr
."bounce = " bounce . cr
."mode = " send-mode x. cr
."Body of the message is " body csr. cr
